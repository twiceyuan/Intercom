apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

retrolambda {
    jvmArgs '-noverify'
}

// 打包时间
def releaseTime() {
    return new Date().format("yyyyMMddHHmmss")
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    // apk输出重命名
    applicationVariants.all { variant ->
        // debug 的 dev 版本不进行输出重命名，否则 Android Studio 的运行将无法定位到输出的 apk 文件
        if (!(variant.productFlavors[0].name.equals("dev") || variant.productFlavors[0].name.equals("prod"))) {
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    def version = defaultConfig.versionName
                    def time = releaseTime()
                    def buildTypeName = variant.buildType.name
                    //noinspection GroovyAssignabilityCheck
                    def flavorName = variant.productFlavors[0].name
                    def newName = "Intercom-v${version}-${flavorName}-${buildTypeName}-${time}.apk"
                    //noinspection GroovyAssignabilityCheck
                    output.outputFile = new File(outputFile.parent, newName)
                }
            }
        }
    }
    //删除 unaligned apk
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.zipAlign != null) {
                output.zipAlign.doLast {
                    output.zipAlign.inputFile.delete()
                }
            }
        }
    }
    defaultConfig {
        applicationId "com.twiceyuan.intercom"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "0.1.0-alpha"
    }
    signingConfigs {
        release {
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file(KEY_PATH)
            storePassword KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        dev {
            minSdkVersion 21
        }
        prod {
            minSdkVersion 14
        }
        publish {
            minSdkVersion 14
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

repositories {
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {

    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile "com.jakewharton:butterknife:${rootProject.ext.butterKnifeVersion}"
    apt "com.jakewharton:butterknife-compiler:${rootProject.ext.butterKnifeVersion}"

    //noinspection GradleDependency
    compile "com.android.support:appcompat-v7:${rootProject.ext.androidSupportVersion}"
    //noinspection GradleDependency
    compile "com.android.support:design:${rootProject.ext.androidSupportVersion}"
    compile "com.google.firebase:firebase-auth:${rootProject.ext.firebaseVersion}"
    compile "com.google.firebase:firebase-messaging:${rootProject.ext.firebaseVersion}"
    compile "com.google.firebase:firebase-crash:${rootProject.ext.firebaseVersion}"
    compile 'com.firebaseui:firebase-ui-database:0.4.0'
    //noinspection GradleDependency
    compile 'com.google.android.gms:play-services-auth:9.0.0'
    // 图片加载
    compile 'com.github.bumptech.glide:glide:3.6.1'
    // 圆形图片
    compile 'de.hdodenhof:circleimageview:1.3.0'
    // RxJava for Android
    compile 'io.reactivex:rxandroid:1.2.1'

    // 第三方编辑框
    compile 'com.rengwuxian.materialedittext:library:2.1.4'

    // Dagger 2
    apt "com.google.dagger:dagger-compiler:${rootProject.ext.daggerVersion}"
    compile "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"
    provided 'javax.annotation:jsr250-api:1.0'

    compile("com.github.twiceyuan:CommonAdapter:${rootProject.ext.commonAdapterVersion}") {
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    compile 'com.github.twiceyuan:AndroidLog:1.0.1'

    // JSON -> Model
    compile 'com.google.code.gson:gson:2.5'
    // 网络请求相关
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'

    // 图片选择
    compile 'com.github.jkwiecien:EasyImage:1.2.4'
    compile 'com.theartofdev.edmodo:android-image-cropper:2.3.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

